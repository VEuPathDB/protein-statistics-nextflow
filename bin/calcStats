#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;
use Bio::Seq;
use Bio::SeqIO;
use Bio::Tools::SeqStats;
use Bio::Tools::pICalculator;



use constant DEBUG => 0;

my ($fastaFile, $outFile, $hydropathyOutput, $proteinSizesOutput);
&GetOptions(
    "fastaFile=s"       => \$fastaFile,
    "outFile=s"       => \$outFile,
    "hydropathyOutput=s" => \$hydropathyOutput,
    "proteinSizesOutput=s" => \$proteinSizesOutput,

    );

unless ($fastaFile && $outFile ) { die "args fastaFile and outfile are required\n" };
unless (-e $fastaFile) { die "file $fastaFile not found\n" };

open (OUT, ">$outFile") or die "Can't open '$outFile' for writing\n";

open (BED, ">$hydropathyOutput") or die "Can't open '$hydropathyOutput' for writing\n";

open (SIZES, ">$proteinSizesOutput") or die "Can't open '$proteinSizesOutput' for writing\n";

my %HYDROPATHY_INDEX = ( 'I' => 4.5,
                         'V' => 4.2,
                         'L' => 3.8,
                         'F' => 2.8,
                         'C' => 2.5,
                         'M' => 1.9,
                         'A' => 1.8,
                         'G' => -0.4,
                         'T' => -0.7,
                         'W' => -0.9,
                         'S' => -0.8,
                         'Y' => -1.3,
                         'P' => -1.6,
                         'H' => -3.2,
                         'E' => -3.5,
                         'Q' => -3.5,
                         'D' => -3.5,
                         'N' => -3.5,
                         'K' => -3.9,
                         'R' => -4.5,);


##########################
##### "main()" ###########

processFile($fastaFile, \*OUT, \*BED, \*SIZES);
 
##########################



###############################################################
## subroutines ################################################

sub processFile {
    my ($inputFile, $fh, $bedFh, $sizesFh) = @_;

    $inputFile =~ s/(.*\.gz)\s*$/gzip -dc < $1|/;
    $inputFile =~ s/(.*\.Z)\s*$/uncompress -c < $1|/;
    
    my $seqIO = Bio::SeqIO->new(-file => $inputFile);
    my $pIcalc = Bio::Tools::pICalculator->new();

    while (my $richSeqObj = $seqIO->next_seq) {
        $richSeqObj->alphabet('protein');

        my $proteinId = $richSeqObj->id();
        processOneSeq($richSeqObj, $proteinId, $pIcalc, $fh);

        hydropathy($richSeqObj, $proteinId, $bedFh);

        print $sizesFh $proteinId, "\t", length($richSeqObj->seq()), "\n";
    }
}

sub processOneSeq {
    my ($seqObj, $id, $pIcalc, $fh) = @_;

    my ($minWt, $maxWt) =
      @{Bio::Tools::SeqStats->get_mol_wt($seqObj)};

    $pIcalc->seq($seqObj);

    my $isoelectricPoint = $pIcalc->iep();

    print $fh "$id\t$isoelectricPoint\t$minWt\t$maxWt\n";
}


sub hydropathy {
    my ($seqObj, $id, $fh) = @_;

    my $proteinSeq = $seqObj->seq();



    my @aminoAcids = split(//, $proteinSeq);
    for(my $i = 0; $i < scalar @aminoAcids; $i++) {

        my $aa = $aminoAcids[$i];

        if(defined $HYDROPATHY_INDEX{$aa}) {
            my $hydropathyScore = $HYDROPATHY_INDEX{$aa};
            my $end = $i + 1;

            print $fh "$id\t$i\t$end\t$hydropathyScore\n";
        }
    }

}
